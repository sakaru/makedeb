# Maintainer: Marcel Campello <marcel.campello@prafrentex.com.br>
# Contributor: David Birks <david@birks.dev>

pkgname='aws-cli-v2'
pkgver='2.9.5'
pkgrel='1'

pkgdesc='Unified command line interface for Amazon Web Services (version 2)'
arch=('amd64')
url='https://github.com/aws/aws-cli/tree/v2'
license=('Apache')

depends=(
  'python3-awscrt'
  'python3-certifi'
  'python3-colorama'
  'python3-cryptography'
  'python3-dateutil'
  'python3-distro'
  'python3-docutils'
  'python3-jmespath'
  'python3-prompt-toolkit'
  'python3-ruamel.yaml'
  'python3-urllib3'
)
makedepends=(
  'python3-build'
  'python3-wheel'
  'python3-setuptools'
  'python3-installer'
  'flit'
)

provides=('aws-cli')
conflicts=('aws-cli' 'aws-cli-git' 'aws-cli-v2-bin')

source=("$pkgname-$pkgver.tar.gz::https://github.com/aws/aws-cli/archive/$pkgver.tar.gz")
sha256sums=('352231c8f9c3f1fee3b864023e40df855e9c7145187a30164414b1f2f511aab4')

build() {
  cd aws-cli-$pkgver
  python3 -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd aws-cli-$pkgver
  python3 -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 bin/aws_bash_completer "$pkgdir/usr/share/bash-completion/completions/aws"
}
