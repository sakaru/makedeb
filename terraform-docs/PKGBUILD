# Maintainer: Sid Karunaratne <sid@karunaratne.net>
pkgname=terraform-docs
pkgver=0.17.0
pkgrel=1
pkgdesc=' Generate documentation from Terraform modules in various output formats'
arch=('amd64')
makedepends=('golang-go')
license=(MIT)
url='https://github.com/terraform-docs/terraform-docs/'
source=("${url}archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=("698537c9cc170f8760cda862d560bfeaa24f2a705bbc70c22a8facbf05a829e0")

prepare() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  go clean -modcache
  mkdir -p build/
}

build() {
  cd "$pkgname-$pkgver"
  export GOPATH="$srcdir/gopath"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -v -o "build/$pkgname"

  # Clean mod cache for makepkg -C
  go clean -modcache
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "build/$pkgname" -t "$pkgdir/usr/bin/"
}
