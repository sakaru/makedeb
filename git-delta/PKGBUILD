# Maintainer: Sid Karunaratne <sid@karunaratne.net>

pkgname=git-delta
_pkgname=delta
pkgver=0.17.0
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('amd64')
url='https://github.com/dandavison/delta'
license=('MIT')
makedepends=('cargo' 'git')
source=("$url/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('7b3d4aa9bad2f3dc920d9855163e81813ea5467cd92939d4781ed3d896d10bb39110265d5294967c79f8451e8a6d3d4def7e42efb4064f3d5fbc5d99e5c245a6')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  # binary
  cargo build --frozen --release --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"
  install -vDm644 etc/completion/completion.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
