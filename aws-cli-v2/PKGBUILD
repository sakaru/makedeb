# Maintainer: Sid Karunaratne <sid@karunaratne.net>

pkgname=aws-cli-v2
pkgver=2.11.15
pkgrel=1

pkgdesc='Unified command line interface for Amazon Web Services (version 2)'
arch=('amd64')
url='https://github.com/aws/aws-cli/tree/v2'
license=('Apache')

depends=(
  'python3-awscrt'
  'python3-certifi'
  'python3-colorama'
  'python3-cryptography'
  'python3-dateutil'
  'python3-distro'
  'python3-docutils'
  'python3-jmespath'
  'python3-prompt-toolkit'
  'python3-ruamel.yaml'
  'python3-urllib3'
)
makedepends=(
  'python3-build'
  'python3-wheel'
  'python3-setuptools'
  'python3-installer'
  'flit'
)

provides=('awscli')
conflicts=('awscli')

source=("https://awscli.amazonaws.com/awscli-$pkgver.tar.gz")
sha256sums=('5ee3ae5cc9210ad15eddb1005d8747a231337b118aff581cfd472c36e1bdfdcd')

build() {
  cd awscli-$pkgver
  python3 -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd awscli-$pkgver
  python3 -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 bin/aws_bash_completer "$pkgdir/usr/share/bash-completion/completions/aws"
}
