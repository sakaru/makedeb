# Maintainer: Sid Karunaratne <sid.karunaratne@pix4d.com>

pkgname=i3status-rust
pkgver=0.33.0
pkgrel=1
pkgdesc='Resourcefriendly and feature-rich replacement for i3status, written in pure Rust'
arch=('amd64')
url='https://github.com/greshake/i3status-rust'
license=('GPL3')
depends=('libpulse-dev' 'libsensors-dev')
makedepends=('rustup' 'pandoc')
optdepends=('alsa-utils: for the volume block'
            'bluez: for the bluetooth block'
            'curl: for the weather block'
            'fakeroot: for the pacman block to show pending updates'
            'ibus: for the ibus block'
            'iw: display wifi signal strength in net block'
            'kdeconnect: for the kdeconnect block'
            'networkmanager: for the networkmanager block'
            'powerline-fonts: for all themes using the Powerline arrow char'
            'pulseaudio: for the volume block'
            'speedtest-cli: for the speedtest block'
            'ttf-font-awesome: for the awesome icons'
            'upower: for the battery block')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/greshake/i3status-rust/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('f8df762b42a76b5f63a7ea84b2446c55ae0ee3e1b40658baf788426433af24ad26cedc1fc676323d74b850ae19d4d7264be1bd2d227e92a5bb53319e84615527')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "x86_64-unknown-linux-gnu"
}


build() {
  cd ${pkgname}-${pkgver}
  cargo xtask generate-manpage
  cargo build --release --locked --features 'pulseaudio maildir'
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}"/usr/bin target/release/i3status-rs
  install -Dm644 -t "${pkgdir}"/usr/share/man/man1 man/i3status-rs.1

  for icon in files/icons/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/${pkgname}/icons "${icon}"
  done

  for theme in files/themes/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/${pkgname}/themes "${theme}"
  done

  for example in examples/*.toml; do
    install -Dm644 -t "${pkgdir}"/usr/share/doc/${pkgname}/examples/ ${example}
  done
}
