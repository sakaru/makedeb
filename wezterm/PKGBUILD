# Maintainer: Sid Karunaratne <sid@karunaratne.net>

pkgname=wezterm
pkgver=20230712
_gitbranch="${pkgver}-072601-f4abf8fd"
pkgrel=1
pkgdesc="A terminal emulator with support for modern features such as fonts with ligatures, hyperlinks, tabs and multiple windows."
arch=('amd64')
url="https://github.com/wez/wezterm"
license=('MIT')
depends=()
makedepends=(
  'cargo'
  'cmake'
  'git'
  'libpkgconf-dev'
  'libssh2-1-dev'
  'python3'
)
depends=(
  'fontconfig'
  'hicolor-icon-theme'
  'libx11-xcb-dev'
  'libxcb-ewmh-dev'
  'libxcb-image0-dev'
  'libxcb-keysyms1'
  'libxkbcommon-x11-dev'
)

optdepends=(
  'python-nautilus: WezTerm context menu in Nautilus'
)

source=(
  "${pkgname}::git+$url#tag=${_gitbranch}"
  "${pkgname}-freetype2::git+https://github.com/wez/freetype2.git"
  "${pkgname}-zlib::git+https://github.com/madler/zlib.git"
  "${pkgname}-harfbuzz::git+https://github.com/harfbuzz/harfbuzz.git"
  "${pkgname}-libpng::git+https://github.com/glennrp/libpng.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$pkgname"
  git submodule init

  git config submodule."harfbuzz/harfbuzz".url "${srcdir}/${pkgname}"-harfbuzz
  git config submodule."freetype/libpng".url "${srcdir}/${pkgname}"-libpng
  git config submodule."deps/freetype/zlib".url "${srcdir}/${pkgname}"-zlib
  git config submodule."freetype2".url "${srcdir}/${pkgname}"-freetype2

  git -c protocol.file.allow=always submodule update --init --recursive
  cargo fetch --locked --target "x86_64-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo build --frozen --release --features distro-defaults
}

check() {
  cd "$pkgname"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen -- --skip "e2e::sftp"
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-gui" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-mux-server" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/strip-ansi-escapes" -t "$pkgdir/usr/bin"
  install -Dm 644 "assets/icon/terminal.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/org.wezfurlong.$pkgname.png"
  install -Dm 644 "assets/$pkgname.desktop" "$pkgdir/usr/share/applications/org.wezfurlong.$pkgname.desktop"
  install -Dm 644 "assets/$pkgname.appdata.xml" "$pkgdir/usr/share/metainfo/org.wezfurlong.$pkgname.appdata.xml"
  install -Dm 644 "assets/$pkgname-nautilus.py" "$pkgdir/usr/share/nautilus-python/extensions/$pkgname-nautilus.py"
  install -Dm 755 "assets/open-$pkgname-here" -t "$pkgdir/usr/bin"
  install -Dm 644 assets/shell-integration/* -t "$pkgdir/etc/profile.d"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 assets/shell-completion/bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 assets/shell-completion/fish "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm 644 assets/shell-completion/zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
